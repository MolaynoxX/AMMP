buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'gradle.plugin.de.fuerstenau:BuildConfigPlugin:1.1.0'
    }
}

plugins {
    id 'de.fuerstenau.buildconfig' version '1.1.0'
}

group 'de.molaynoxx'
version '0.0.1-alpha'

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'idea'

sourceCompatibility = 1.8
def portable = isPortable()

repositories {
    mavenCentral()
}

buildConfig {
    packageName = 'de.molaynoxx.amperfi'
    clsName = 'BuildConfig'
    buildConfigField 'boolean', 'IS_PORTABLE', portable
}

jar {
    manifest {
        attributes('Implementation-Title': 'Gradle Jar File with dependencies',
                'Implementation-Version': version,
                'Main-Class': 'de.molaynoxx.amperfi.Amperfi')
    }
    baseName = project.name + (portable == 'true' ? '-portable' : '')
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

dependencies {
    compile group: 'com.querydsl', name: 'querydsl-sql', version: '4.0.7'
    compile group: 'com.querydsl', name: 'querydsl-sql-codegen', version: '4.0.7'
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.6.1'
    compile group: 'org.xerial', name: 'sqlite-jdbc', version: '3.8.11.2'
    compile 'commons-io:commons-io:2.4'
    compile fileTree(dir: 'libs/', include: '*.jar')
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

def isPortable() {
    def value = project.getProperties().get("portable")
    return value != null ? value : "false"
}